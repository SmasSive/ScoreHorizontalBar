apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: com.novoda.gradle.release.ReleasePlugin

android {
  compileSdkVersion 26

  defaultConfig {
    minSdkVersion 16
    targetSdkVersion 26
    versionCode 1
    versionName "1.0"
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  lintOptions {
    abortOnError false
  }
}

dependencies {
  implementation 'com.android.support:appcompat-v7:26.1.0'
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

publish {
  userOrg = 'smassive'
  groupId = 'com.smassive'
  artifactId = 'scorehorizontalbar'
  publishVersion = '1.0.0'
  desc = 'Simple score horizontal bar view'
  website = 'https://github.com/SmasSive/ScoreHorizontalBar'
}

repositories {
  mavenCentral()
}

// Hack for: https://github.com/novoda/bintray-release/issues/71
tasks.withType(Javadoc) {
  excludes = ['**/*.kt']
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier = 'sources'
}

task javadoc(type: Javadoc) {
  source = android.sourceSets.main.java.srcDirs
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}
artifacts {
  archives javadocJar
  archives sourcesJar
}
